 agent_of_finding_component_dependencies
-> nrel_main_idtf:
	[агентная scp-программа поиска зависимостей между компонентами] (* <- lang_ru;; *);
	[agent scp-program of finding component dependencies] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_finding_component_dependencies 
	(*
	-> rrel_params: .agent_of_finding_component_dependencies_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_component_dependencies_operator_set 
		(*
		-> rrel_init: .agent_of_finding_component_dependencies_operatorA1
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_finding_component_dependencies_operatorA2;;
			=> nrel_else: .agent_of_finding_component_dependencies_operator_return;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA2
        		(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_find_component_dependencies;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_finding_component_dependencies_operatorA3;;
			=> nrel_else: .agent_of_finding_component_dependencies_operator_return;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA3
        		(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: component;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

			=> nrel_then: .agent_of_finding_component_dependencies_operatorA4;;
			=> nrel_else: .agent_of_finding_component_dependencies_operator_return;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA4
        		(*
			<- genElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: answer;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: component;;

			=> nrel_goto: .agent_of_finding_component_dependencies_operatorA5;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA5
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_finding_dependent_components;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_finding_component_dependencies_operatorA4_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: component;;
				-> rrel_2: rrel_fixed: rrel_scp_var: answer;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

		  	=> nrel_goto: .agent_of_finding_component_dependencies_operatorA6;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA6
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

		  	=> nrel_goto: .agent_of_finding_component_dependencies_operatorA7;;
			*);;

		-> .agent_of_finding_component_dependencies_operatorA7
		 	(*
			<- genElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: rrel_const: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: answer;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: rrel_const: _arc1;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;
		  	
		  	=> nrel_goto: .agent_of_finding_component_dependencies_operator_return;;	
		  	*);;

		-> .agent_of_finding_component_dependencies_operator_return
			(*
			<- return;;
			*);;
		*);;
	*);;
